OBJECT Table 274 Bank Acc. Reconciliation Line
{
  OBJECT-PROPERTIES
  {
    Date=23/09/13;
    Time=12:00:00;
    Version List=NAVW17.10;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               BankAccRecon.GET("Bank Account No.","Statement No.");
               "Applied Entries" := 0;
               VALIDATE("Applied Amount",0);
             END;

    OnModify=BEGIN
               IF xRec."Statement Amount" <> "Statement Amount" THEN
                 RemoveApplication(Type);
             END;

    OnDelete=BEGIN
               RemoveApplication(Type);
               ClearPostExchEntries;
             END;

    OnRename=BEGIN
               ERROR(Text000,TABLECAPTION);
             END;

    CaptionML=[ENU=Bank Acc. Reconciliation Line;
               FRA=Ligne rapprochement bancaire];
    PasteIsValid=No;
  }
  FIELDS
  {
    { 1   ;   ;Bank Account No.    ;Code20        ;TableRelation="Bank Account";
                                                   CaptionML=[ENU=Bank Account No.;
                                                              FRA=N¯ compte bancaire] }
    { 2   ;   ;Statement No.       ;Code20        ;TableRelation="Bank Acc. Reconciliation"."Statement No." WHERE (Bank Account No.=FIELD(Bank Account No.));
                                                   CaptionML=[ENU=Statement No.;
                                                              FRA=N¯ relevÇ] }
    { 3   ;   ;Statement Line No.  ;Integer       ;CaptionML=[ENU=Statement Line No.;
                                                              FRA=N¯ ligne relevÇ] }
    { 4   ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              FRA=N¯ document] }
    { 5   ;   ;Transaction Date    ;Date          ;CaptionML=[ENU=Transaction Date;
                                                              FRA=Date transaction] }
    { 6   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              FRA=DÇsignation] }
    { 7   ;   ;Statement Amount    ;Decimal       ;OnValidate=BEGIN
                                                                Difference := "Statement Amount" - "Applied Amount";
                                                              END;

                                                   CaptionML=[ENU=Statement Amount;
                                                              FRA=Montant relevÇ];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 8   ;   ;Difference          ;Decimal       ;OnValidate=BEGIN
                                                                "Statement Amount" := "Applied Amount" + Difference;
                                                              END;

                                                   CaptionML=[ENU=Difference;
                                                              FRA=DiffÇrence];
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 9   ;   ;Applied Amount      ;Decimal       ;OnValidate=BEGIN
                                                                Difference := "Statement Amount" - "Applied Amount";
                                                              END;

                                                   OnLookup=BEGIN
                                                              DisplayApplication;
                                                            END;

                                                   CaptionML=[ENU=Applied Amount;
                                                              FRA=Montant lettrÇ];
                                                   Editable=No;
                                                   AutoFormatType=1;
                                                   AutoFormatExpr=GetCurrencyCode }
    { 10  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                IF (Type <> xRec.Type) AND
                                                                   ("Applied Entries" <> 0)
                                                                THEN
                                                                  IF CONFIRM(Text001,FALSE) THEN BEGIN
                                                                    RemoveApplication(xRec.Type);
                                                                    VALIDATE("Applied Amount",0);
                                                                    "Applied Entries" := 0;
                                                                    "Check No." := '';
                                                                  END ELSE
                                                                    ERROR(Text002);
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              FRA=Type];
                                                   OptionCaptionML=[ENU=Bank Account Ledger Entry,Check Ledger Entry,Difference;
                                                                    FRA=Banque,Chäque,DiffÇrence];
                                                   OptionString=Bank Account Ledger Entry,Check Ledger Entry,Difference }
    { 11  ;   ;Applied Entries     ;Integer       ;OnLookup=BEGIN
                                                              DisplayApplication;
                                                            END;

                                                   CaptionML=[ENU=Applied Entries;
                                                              FRA=êcritures lettrÇes];
                                                   Editable=No }
    { 12  ;   ;Value Date          ;Date          ;CaptionML=[ENU=Value Date;
                                                              FRA=Date de valeur] }
    { 13  ;   ;Ready for Application;Boolean      ;CaptionML=[ENU=Ready for Application;
                                                              FRA=Pràt Ö lettrer] }
    { 14  ;   ;Check No.           ;Code20        ;CaptionML=[ENU=Check No.;
                                                              FRA=N¯ chäque] }
    { 15  ;   ;Payer Information   ;Text50        ;CaptionML=[ENU=Payer Information;
                                                              FRA=Informations payeur] }
    { 16  ;   ;Transaction Information;Text100    ;CaptionML=[ENU=Transaction Information;
                                                              FRA=Informations transaction] }
    { 17  ;   ;Posting Exch. Entry No.;Integer    ;TableRelation="Posting Exch.";
                                                   CaptionML=[ENU=Posting Exch. Entry No.;
                                                              FRA=N¯ Çcriture Çchge compta.];
                                                   Editable=No }
    { 18  ;   ;Posting Exch. Line No.;Integer     ;CaptionML=[ENU=Posting Exch. Line No.;
                                                              FRA=N¯ ligne Çchge compta.];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Bank Account No.,Statement No.,Statement Line No.;
                                                   SumIndexFields=Statement Amount,Difference;
                                                   MaintainSIFTIndex=No;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=You cannot rename a %1.;FRA=Vous ne pouvez pas renommer l''enregistrement %1.';
      Text001@1001 : TextConst 'ENU=Delete application?;FRA=Souhaitez-vous supprimer le lettrage ?';
      Text002@1002 : TextConst 'ENU=Update canceled.;FRA=Mise Ö jour annulÇe.';
      BankAccLedgEntry@1003 : Record 271;
      CheckLedgEntry@1004 : Record 272;
      BankAccRecon@1005 : Record 273;
      BankAccSetStmtNo@1006 : Codeunit 375;
      CheckSetStmtNo@1007 : Codeunit 376;

    LOCAL PROCEDURE RemoveApplication@1(AppliedType@1000 : Option);
    BEGIN
      CASE AppliedType OF
        Type::"Bank Account Ledger Entry":
          BEGIN
            BankAccLedgEntry.RESET;
            BankAccLedgEntry.SETCURRENTKEY("Bank Account No.",Open);
            BankAccLedgEntry.SETRANGE("Bank Account No.","Bank Account No.");
            BankAccLedgEntry.SETRANGE(Open,TRUE);
            BankAccLedgEntry.SETRANGE(
              "Statement Status",BankAccLedgEntry."Statement Status"::"Bank Acc. Entry Applied");
            BankAccLedgEntry.SETRANGE("Statement No.","Statement No.");
            BankAccLedgEntry.SETRANGE("Statement Line No.","Statement Line No.");
            BankAccLedgEntry.LOCKTABLE;
            CheckLedgEntry.LOCKTABLE;
            IF BankAccLedgEntry.FIND('-') THEN
              REPEAT
                BankAccSetStmtNo.RemoveReconNo(BankAccLedgEntry,Rec,TRUE);
              UNTIL BankAccLedgEntry.NEXT = 0;
            "Applied Entries" := 0;
            VALIDATE("Applied Amount",0);
            MODIFY;
          END;
        Type::"Check Ledger Entry":
          BEGIN
            CheckLedgEntry.RESET;
            CheckLedgEntry.SETCURRENTKEY("Bank Account No.",Open);
            CheckLedgEntry.SETRANGE("Bank Account No.","Bank Account No.");
            CheckLedgEntry.SETRANGE(Open,TRUE);
            CheckLedgEntry.SETRANGE(
              "Statement Status",CheckLedgEntry."Statement Status"::"Check Entry Applied");
            CheckLedgEntry.SETRANGE("Statement No.","Statement No.");
            CheckLedgEntry.SETRANGE("Statement Line No.","Statement Line No.");
            BankAccLedgEntry.LOCKTABLE;
            CheckLedgEntry.LOCKTABLE;
            IF CheckLedgEntry.FIND('-') THEN
              REPEAT
                CheckSetStmtNo.RemoveReconNo(CheckLedgEntry,Rec,TRUE);
              UNTIL CheckLedgEntry.NEXT = 0;
            "Applied Entries" := 0;
            VALIDATE("Applied Amount",0);
            "Check No." := '';
            MODIFY;
          END;
      END;
    END;

    LOCAL PROCEDURE DisplayApplication@2();
    BEGIN
      CASE Type OF
        Type::"Bank Account Ledger Entry":
          BEGIN
            BankAccLedgEntry.RESET;
            BankAccLedgEntry.SETCURRENTKEY("Bank Account No.",Open);
            BankAccLedgEntry.SETRANGE("Bank Account No.","Bank Account No.");
            BankAccLedgEntry.SETRANGE(Open,TRUE);
            BankAccLedgEntry.SETRANGE(
              "Statement Status",BankAccLedgEntry."Statement Status"::"Bank Acc. Entry Applied");
            BankAccLedgEntry.SETRANGE("Statement No.","Statement No.");
            BankAccLedgEntry.SETRANGE("Statement Line No.","Statement Line No.");
            PAGE.RUN(0,BankAccLedgEntry);
          END;
        Type::"Check Ledger Entry":
          BEGIN
            CheckLedgEntry.RESET;
            CheckLedgEntry.SETCURRENTKEY("Bank Account No.",Open);
            CheckLedgEntry.SETRANGE("Bank Account No.","Bank Account No.");
            CheckLedgEntry.SETRANGE(Open,TRUE);
            CheckLedgEntry.SETRANGE(
              "Statement Status",CheckLedgEntry."Statement Status"::"Check Entry Applied");
            CheckLedgEntry.SETRANGE("Statement No.","Statement No.");
            CheckLedgEntry.SETRANGE("Statement Line No.","Statement Line No.");
            PAGE.RUN(0,CheckLedgEntry);
          END;
      END;
    END;

    PROCEDURE GetCurrencyCode@3() : Code[10];
    VAR
      BankAcc@1000 : Record 270;
    BEGIN
      IF "Bank Account No." = BankAcc."No." THEN
        EXIT(BankAcc."Currency Code");

      IF BankAcc.GET("Bank Account No.") THEN
        EXIT(BankAcc."Currency Code");

      EXIT('');
    END;

    PROCEDURE GetNextLineNo@4(BankAccNo@1000 : Code[20];StatementNo@1001 : Code[20]) LocalLineNo : Integer;
    BEGIN
      SETRANGE("Bank Account No.",BankAccNo);
      SETRANGE("Statement No.",StatementNo);
      IF FINDLAST THEN
        LocalLineNo := "Statement Line No.";
      LocalLineNo += 10000;
    END;

    PROCEDURE GetStyle@35() : Text;
    BEGIN
      IF "Applied Entries" <> 0 THEN
        EXIT('Favorable');

      EXIT('');
    END;

    LOCAL PROCEDURE ClearPostExchEntries@5();
    VAR
      PostExchField@1000 : Record 1221;
    BEGIN
      PostExchField.SETRANGE("Posting Exch. No.","Posting Exch. Entry No.");
      PostExchField.SETRANGE("Line No.","Posting Exch. Line No.");
      PostExchField.DELETEALL;
    END;

    BEGIN
    END.
  }
}

