OBJECT Report 10864 Suggest Customer Payments
{
  OBJECT-PROPERTIES
  {
    Date=02/12/13;
    Time=12:00:00;
    Version List=NAVFR7.10.00.35800;
  }
  PROPERTIES
  {
    Permissions=TableData 21=rm;
    CaptionML=[ENU=Suggest Customer Payments;
               FRA=Proposer rŠglements client];
    ProcessingOnly=Yes;
  }
  DATASET
  {
    { 6836;    ;DataItem;                    ;
               DataItemTable=Table18;
               DataItemTableView=SORTING(No.);
               OnPreDataItem=BEGIN
                               IF LastDueDateToPayReq = 0D THEN
                                 ERROR(Text000);
                               IF PostingDate = 0D THEN
                                 ERROR(Text001);

                               GenPayLineInserted := FALSE;
                               SeveralCurrencies := FALSE;
                               MessageText := '';

                               IF UsePaymentDisc AND (LastDueDateToPayReq < WORKDATE) THEN
                                 IF NOT
                                    CONFIRM(
                                      Text003+
                                      Text004,FALSE,
                                      WORKDATE)
                                 THEN
                                   ERROR(Text005);

                               Cust2.COPYFILTERS(Customer);
                               Window.OPEN(Text006);

                               NextEntryNo := 1;
                             END;

               OnAfterGetRecord=BEGIN
                                  Window.UPDATE(1,"No.");
                                  GetCustLedgEntries(TRUE,FALSE);
                                  GetCustLedgEntries(FALSE,FALSE);
                                  CheckAmounts(FALSE);
                                END;

               OnPostDataItem=BEGIN
                                IF UsePaymentDisc THEN BEGIN
                                  RESET;
                                  COPYFILTERS(Cust2);
                                  Window.OPEN(Text007);
                                  IF FIND('-') THEN
                                    REPEAT
                                      Window.UPDATE(1,"No.");
                                      PayableCustLedgEntry.SETRANGE("Vendor No.","No.");
                                      GetCustLedgEntries(TRUE,TRUE);
                                      GetCustLedgEntries(FALSE,TRUE);
                                      CheckAmounts(TRUE);
                                    UNTIL NEXT = 0;
                                END;

                                GenPayLine.LOCKTABLE;
                                GenPayLine.SETRANGE("No.",GenPayLine."No.");
                                IF GenPayLine.FIND('+') THEN BEGIN
                                  FirstLineNo := GenPayLine."Line No.";
                                  LastLineNo := GenPayLine."Line No.";
                                  GenPayLine.INIT;
                                END;

                                Window.OPEN(Text008);

                                PayableCustLedgEntry.RESET;
                                PayableCustLedgEntry.SETRANGE(Priority,1,2147483647);
                                MakeGenPayLines;
                                PayableCustLedgEntry.RESET;
                                PayableCustLedgEntry.SETRANGE(Priority,0);
                                MakeGenPayLines;
                                PayableCustLedgEntry.RESET;
                                PayableCustLedgEntry.DELETEALL;
                                Window.CLOSE;

                                IF GenPayLineInserted AND (Customer.GETFILTER("Partner Type") <> '')  THEN BEGIN
                                  GenPayHead."Partner Type" := Customer."Partner Type";
                                  GenPayHead.MODIFY;
                                END;
                                ShowMessage(MessageText);
                              END;

               ReqFilterFields=No.,Payment Method Code }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=No;
      RefreshOnActivate=No;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             FRA=Options] }

      { 1120001;2;Field     ;
                  Name=LastPaymentDate;
                  CaptionML=[ENU=Last Payment Date;
                             FRA=DerniŠre date ‚ch‚ance];
                  SourceExpr=LastDueDateToPayReq }

      { 1120002;2;Field     ;
                  CaptionML=[ENU=Find Payment Discounts;
                             FRA=Rechercher les escomptes];
                  SourceExpr=UsePaymentDisc;
                  MultiLine=Yes }

      { 1120000;2;Field     ;
                  Name=SummarizePer;
                  CaptionML=[ENU=Summarize per;
                             FRA=Totaliser par];
                  OptionCaptionML=[ENU=" ,Customer,Due date";
                                   FRA=" ,Client,Date d'‚ch‚ance"];
                  SourceExpr=SummarizePer }

      { 1120005;2;Field     ;
                  Name=CurrencyFilter;
                  CaptionML=[ENU=Currency Filter;
                             FRA=Filtre devise];
                  SourceExpr=CurrencyFilter;
                  TableRelation=Currency;
                  Editable=FALSE }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      Text000@1120000 : TextConst 'ENU=Please enter the last payment date.;FRA=Veuillez entrer la derniŠre date d''‚ch‚ance.';
      Text001@1120001 : TextConst 'ENU=Please enter the posting date.;FRA=Veuillez entrer une date de comptabilisation.';
      Text002@1120002 : TextConst 'ENU=Please enter a Starting Document No.;FRA=Veuillez renseigner le champ Nø document d‚but.';
      Text003@1120003 : TextConst 'ENU=The selected last due date is earlier than %1.\\;FRA=La date de derniŠre ‚ch‚ance s‚lectionn‚e est ant‚rieure au %1.\\';
      Text004@1120004 : TextConst 'ENU=Do you still want to run the batch job?;FRA=Souhaitez-vous tout de mˆme ex‚cuter ce traitement par lots ?';
      Text005@1120005 : TextConst 'ENU=The batch job was interrupted.;FRA=Le traitement par lots a ‚t‚ interrompu.';
      Text006@1120006 : TextConst 'ENU=Processing customers     #1##########;FRA=Traitement des clients                  #1##########';
      Text007@1120007 : TextConst 'ENU=Processing customers for payment discounts #1##########;FRA=Traitement des escomptes client         #1##########';
      Text008@1120008 : TextConst 'ENU=Inserting payment journal lines #1##########;FRA=Insertion des lignes f. paiement        #1##########';
      Text009@1120009 : TextConst 'ENU=%1 must be G/L Account or Bank Account.;FRA=%1 doit ˆtre un compte g‚n‚ral ou un compte bancaire.';
      Text010@1120010 : TextConst 'ENU=%1 must be filled only when %2 is Bank Account.;FRA=%1 ne doit ˆtre renseign‚ que lorsque %2 est un compte bancaire.';
      Text011@1120011 : TextConst 'ENU=Use Customer Priority must be activated when the value in the Amount Available field is not 0.;FRA=Le champ Utiliser priorit‚ client doit ˆtre activ‚ lorsque la valeur du champ Montant disponible est diff‚rente de 0.';
      Text012@1120012 : TextConst 'ENU=Starting Document No. must contain a number.;FRA=Le champ Nø document d‚but doit contenir un num‚ro.';
      Text013@1120013 : TextConst 'ENU=Use Customer Priority must be activated when the value in the Amount Available Amount (LCY) field is not 0.;FRA=Le champ Utiliser priorit‚ client doit ˆtre activ‚ lorsque la valeur du champ Montant disponible DS est diff‚rente de 0.';
      Text014@1120014 : TextConst 'ENU=Payment to customer %1;FRA=Paiement au client %1';
      Text015@1120015 : TextConst 'ENU=Payment of %1 %2;FRA=Paiement de %1 %2';
      Text016@1120016 : TextConst 'ENU=" is already applied to %1 %2 for customer %3.";FRA=" est d‚j… lettr‚(e) avec %1 %2 pour le client %3."';
      Text017@1120017 : TextConst 'ENU="When %1 = %2 and you have not placed a check mark in the Summarize per Customer field,\";FRA="Lorsque %1 = %2 et que vous n''avez pas activ‚ le champ Totaliser par client,\"';
      Text018@1120018 : TextConst 'ENU=then you must place a check mark in New Doc. No. per Line.;FRA=vous devez donc activer le champ Nouveau nø document par ligne.';
      Text019@1120019 : TextConst 'ENU=You have only created suggested customer payment lines for the %1 %2.\;FRA=Vous avez seulement cr‚‚ des lignes proposition paiements client pour %1 %2.\';
      Text020@1120020 : TextConst 'ENU=There are, however, other open customer ledger entries in currencies other than %2.;FRA=Il existe cependant d''autres ‚critures comptables client ouvertes dans d''autres devises que la devise %2.';
      Text021@1120021 : TextConst 'ENU=There are no other open customer ledger entries in other currencies.;FRA=Il n''existe aucune autre ‚criture comptable client ouverte dans une autre devise.';
      Text022@1120022 : TextConst 'ENU=You have created suggested customer payment lines for all currencies.;FRA=Vous avez cr‚‚ des lignes proposition paiements client pour toutes les devises.';
      Cust2@1120023 : Record 18;
      GenPayHead@1120024 : Record 10865;
      GenPayLine@1120025 : Record 10866;
      CustLedgEntry@1120026 : Record 21;
      GLAcc@1120027 : Record 15;
      BankAcc@1120028 : Record 270;
      PayableCustLedgEntry@1120029 : TEMPORARY Record 317;
      TempPaymentPostBuffer@1120030 : TEMPORARY Record 10864;
      OldTempPaymentPostBuffer@1120031 : TEMPORARY Record 10864;
      NoSeriesMgt@1120032 : Codeunit 396;
      CustEntryEdit@1120033 : Codeunit 103;
      Window@1120035 : Dialog;
      UsePaymentDisc@1120036 : Boolean;
      PostingDate@1120037 : Date;
      LastDueDateToPayReq@1120038 : Date;
      NextDocNo@1120039 : Code[20];
      SummarizePer@1120040 : ' ,Customer,Due date';
      FirstLineNo@1120041 : Integer;
      LastLineNo@1120042 : Integer;
      NextEntryNo@1120043 : Integer;
      MessageText@1120044 : Text[250];
      GenPayLineInserted@1120045 : Boolean;
      SeveralCurrencies@1120046 : Boolean;
      Text023@1120047 : TextConst 'ENU=" ,Computer Check,Manual Check";FRA=" ,Informatique,Manuel"';
      PaymentClass@1120048 : Record 10860;
      CurrencyFilter@1120034 : Code[10];

    PROCEDURE SetGenPayLine@1120005(NewGenPayLine@1120000 : Record 10865);
    BEGIN
      GenPayHead := NewGenPayLine;
      GenPayLine."No." := NewGenPayLine."No.";
      PaymentClass.GET(GenPayHead."Payment Class");
      PostingDate := GenPayHead."Posting Date";
      CurrencyFilter := GenPayHead."Currency Code";
    END;

    PROCEDURE GetCustLedgEntries@1120004(Positive@1120000 : Boolean;Future@1120001 : Boolean);
    BEGIN
      CustLedgEntry.RESET;
      CustLedgEntry.SETCURRENTKEY("Customer No.",Open,Positive,"Due Date");
      CustLedgEntry.SETRANGE("Customer No.",Customer."No.");
      CustLedgEntry.SETRANGE(Open,TRUE);
      CustLedgEntry.SETRANGE(Positive,Positive);
      CustLedgEntry.SETRANGE("Currency Code",CurrencyFilter);
      CustLedgEntry.SETRANGE("Applies-to ID", '');
      IF Future THEN BEGIN
        CustLedgEntry.SETRANGE("Due Date",LastDueDateToPayReq+1,31129999D);
        CustLedgEntry.SETRANGE("Pmt. Discount Date",PostingDate,LastDueDateToPayReq);
        CustLedgEntry.SETFILTER("Original Pmt. Disc. Possible",'<0');
      END ELSE
        CustLedgEntry.SETRANGE("Due Date",0D,LastDueDateToPayReq);
      CustLedgEntry.SETRANGE("On Hold",'');
      IF CustLedgEntry.FIND('-') THEN
        REPEAT
          SaveAmount;
        UNTIL CustLedgEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE SaveAmount@1120003();
    BEGIN
      WITH GenPayLine DO BEGIN
        "Account Type" := "Account Type"::Customer;
        VALIDATE("Account No.",CustLedgEntry."Customer No.");
        "Posting Date" := CustLedgEntry."Posting Date";
        "Currency Factor" := CustLedgEntry."Adjusted Currency Factor";
        IF "Currency Factor" = 0 THEN
          "Currency Factor" := 1;
        VALIDATE("Currency Code",CustLedgEntry."Currency Code");
        CustLedgEntry.CALCFIELDS("Remaining Amount");
        IF ((CustLedgEntry."Document Type" = CustLedgEntry."Document Type"::"Credit Memo") AND
            (CustLedgEntry."Remaining Pmt. Disc. Possible" <> 0) OR
           (CustLedgEntry."Document Type" = CustLedgEntry."Document Type"::Invoice)) AND
            (PostingDate <= CustLedgEntry."Pmt. Discount Date") AND UsePaymentDisc
        THEN
          Amount := -(CustLedgEntry."Remaining Amount" - CustLedgEntry."Original Pmt. Disc. Possible")
        ELSE
          Amount := -CustLedgEntry."Remaining Amount";
        VALIDATE(Amount);
      END;

      PayableCustLedgEntry."Vendor No." := CustLedgEntry."Customer No.";
      PayableCustLedgEntry."Entry No." := NextEntryNo;
      PayableCustLedgEntry."Vendor Ledg. Entry No." := CustLedgEntry."Entry No.";
      PayableCustLedgEntry.Amount := GenPayLine.Amount;
      PayableCustLedgEntry."Amount (LCY)" := GenPayLine."Amount (LCY)";
      PayableCustLedgEntry.Positive := (PayableCustLedgEntry.Amount > 0);
      PayableCustLedgEntry.Future := (CustLedgEntry."Due Date" > LastDueDateToPayReq);
      PayableCustLedgEntry."Currency Code" := CustLedgEntry."Currency Code";
      PayableCustLedgEntry."Due Date" := CustLedgEntry."Due Date";
      PayableCustLedgEntry.INSERT;
      NextEntryNo := NextEntryNo + 1;
    END;

    PROCEDURE CheckAmounts@1120002(Future@1120000 : Boolean);
    VAR
      CurrencyBalance@1120001 : Decimal;
      PrevCurrency@1120002 : Code[10];
    BEGIN
      PayableCustLedgEntry.SETRANGE("Vendor No.",Customer."No.");
      PayableCustLedgEntry.SETRANGE(Future,Future);
      IF PayableCustLedgEntry.FIND('-') THEN BEGIN
        PrevCurrency := PayableCustLedgEntry."Currency Code";
        REPEAT
          IF PayableCustLedgEntry."Currency Code" <> PrevCurrency THEN BEGIN
            IF CurrencyBalance < 0 THEN BEGIN
              PayableCustLedgEntry.SETRANGE("Currency Code",PrevCurrency);
              PayableCustLedgEntry.DELETEALL;
              PayableCustLedgEntry.SETRANGE("Currency Code");
            END;
            CurrencyBalance := 0;
            PrevCurrency := PayableCustLedgEntry."Currency Code";
          END;
          CurrencyBalance := CurrencyBalance + PayableCustLedgEntry."Amount (LCY)"
        UNTIL PayableCustLedgEntry.NEXT = 0;
        IF CurrencyBalance > 0 THEN BEGIN
          PayableCustLedgEntry.SETRANGE("Currency Code",PrevCurrency);
          PayableCustLedgEntry.DELETEALL;
          PayableCustLedgEntry.SETRANGE("Currency Code");
        END;
      END;
      PayableCustLedgEntry.RESET;
    END;

    LOCAL PROCEDURE MakeGenPayLines@1120001();
    VAR
      SEPADirectDebitMandate@1120002 : Record 1230;
      GenPayLine3@1120000 : Record 81;
      EntryNo@1120001 : Integer;
    BEGIN
      TempPaymentPostBuffer.DELETEALL;

      IF PayableCustLedgEntry.FIND('-') THEN
        REPEAT
          PayableCustLedgEntry.SETRANGE("Vendor No.",PayableCustLedgEntry."Vendor No.");
          PayableCustLedgEntry.FIND('-');
          REPEAT
            CustLedgEntry.GET(PayableCustLedgEntry."Vendor Ledg. Entry No.");
            TempPaymentPostBuffer."Account No." := CustLedgEntry."Customer No.";
            TempPaymentPostBuffer."Currency Code" := CustLedgEntry."Currency Code";
            IF SummarizePer = SummarizePer::"Due date" THEN
              TempPaymentPostBuffer."Due Date" := CustLedgEntry."Due Date";

            TempPaymentPostBuffer."Dimension Entry No." := 0;
            TempPaymentPostBuffer."Global Dimension 1 Code" := '';
            TempPaymentPostBuffer."Global Dimension 2 Code" := '';

            IF SummarizePer IN [SummarizePer::Customer,SummarizePer::"Due date"] THEN BEGIN
              TempPaymentPostBuffer."Auxiliary Entry No." := 0;
              IF TempPaymentPostBuffer.FIND THEN BEGIN
                TempPaymentPostBuffer.Amount := TempPaymentPostBuffer.Amount + PayableCustLedgEntry.Amount;
                TempPaymentPostBuffer."Amount (LCY)" := TempPaymentPostBuffer."Amount (LCY)" + PayableCustLedgEntry."Amount (LCY)";
                TempPaymentPostBuffer.MODIFY;
              END ELSE BEGIN
                LastLineNo := LastLineNo + 10000;
                TempPaymentPostBuffer."Payment Line No." := LastLineNo;
                IF PaymentClass."Line No. Series" = '' THEN
                  NextDocNo := GenPayHead."No." + '/' + FORMAT(LastLineNo)
                ELSE
                  NextDocNo := NoSeriesMgt.GetNextNo(PaymentClass."Line No. Series",PostingDate,FALSE);
                TempPaymentPostBuffer."Document No." := NextDocNo;
                NextDocNo := INCSTR(NextDocNo);
                TempPaymentPostBuffer.Amount := PayableCustLedgEntry.Amount;
                TempPaymentPostBuffer."Amount (LCY)" := PayableCustLedgEntry."Amount (LCY)";
                Window.UPDATE(1,CustLedgEntry."Customer No.");
                TempPaymentPostBuffer.INSERT;
              END;
              CustLedgEntry."Applies-to ID" := TempPaymentPostBuffer."Document No.";
              CustEntryEdit.RUN(CustLedgEntry)
            END ELSE BEGIN
              GenPayLine3.RESET;
              GenPayLine3.SETCURRENTKEY(
                "Account Type","Account No.","Applies-to Doc. Type","Applies-to Doc. No.");
              GenPayLine3.SETRANGE("Account Type",GenPayLine3."Account Type"::Customer);
              GenPayLine3.SETRANGE("Account No.",CustLedgEntry."Customer No.");
              GenPayLine3.SETRANGE("Applies-to Doc. Type",CustLedgEntry."Document Type");
              GenPayLine3.SETRANGE("Applies-to Doc. No.",CustLedgEntry."Document No.");
              IF GenPayLine3.FIND('-') THEN
                GenPayLine3.FIELDERROR(
                  "Applies-to Doc. No.",
                  STRSUBSTNO(
                    Text016,
                    CustLedgEntry."Document Type",CustLedgEntry."Document No.",
                    CustLedgEntry."Customer No."));

              TempPaymentPostBuffer."Applies-to Doc. Type" := CustLedgEntry."Document Type";
              TempPaymentPostBuffer."Applies-to Doc. No." := CustLedgEntry."Document No.";
              TempPaymentPostBuffer."Currency Factor" := CustLedgEntry."Adjusted Currency Factor";
              TempPaymentPostBuffer.Amount := PayableCustLedgEntry.Amount;
              TempPaymentPostBuffer."Amount (LCY)" := PayableCustLedgEntry."Amount (LCY)";
              TempPaymentPostBuffer."Global Dimension 1 Code" := CustLedgEntry."Global Dimension 1 Code";
              TempPaymentPostBuffer."Global Dimension 2 Code" := CustLedgEntry."Global Dimension 2 Code";
              TempPaymentPostBuffer."Auxiliary Entry No." := CustLedgEntry."Entry No.";
              Window.UPDATE(1,CustLedgEntry."Customer No.");
              TempPaymentPostBuffer.INSERT;
            END;
            CustLedgEntry.CALCFIELDS(CustLedgEntry."Remaining Amount");
            CustLedgEntry."Amount to Apply":=CustLedgEntry."Remaining Amount";
            CustEntryEdit.RUN(CustLedgEntry);
          UNTIL PayableCustLedgEntry.NEXT = 0;
          PayableCustLedgEntry.SETFILTER("Vendor No.",'>%1',PayableCustLedgEntry."Vendor No.");
        UNTIL NOT PayableCustLedgEntry.FIND('-');

      CLEAR(OldTempPaymentPostBuffer);
      TempPaymentPostBuffer.SETCURRENTKEY("Document No.");
      IF TempPaymentPostBuffer.FIND('-') THEN
        REPEAT
          WITH GenPayLine DO BEGIN
            INIT;
            Window.UPDATE(1,TempPaymentPostBuffer."Account No.");
            IF SummarizePer = SummarizePer::" " THEN BEGIN
              LastLineNo := LastLineNo + 10000;
              "Line No." := LastLineNo;
              IF PaymentClass."Line No. Series" = '' THEN
                NextDocNo := GenPayHead."No." + '/' + FORMAT(GenPayLine."Line No.")
              ELSE
                NextDocNo := NoSeriesMgt.GetNextNo(PaymentClass."Line No. Series",PostingDate,FALSE);
            END ELSE BEGIN
              "Line No." := TempPaymentPostBuffer."Payment Line No.";
              NextDocNo := TempPaymentPostBuffer."Document No.";
            END;
            "Document No." := NextDocNo;
            GenPayLine."Applies-to ID" := "Document No.";
            OldTempPaymentPostBuffer := TempPaymentPostBuffer;
            OldTempPaymentPostBuffer."Document No." := "Document No.";
            IF SummarizePer = SummarizePer::" " THEN BEGIN
              CustLedgEntry.GET(TempPaymentPostBuffer."Auxiliary Entry No.");
              CustLedgEntry."Applies-to ID" := NextDocNo;
              CustLedgEntry.MODIFY;
            END;
            "Account Type" := "Account Type"::Customer;
            VALIDATE("Account No.",TempPaymentPostBuffer."Account No.");
            "Currency Code" := TempPaymentPostBuffer."Currency Code";
            Amount := TempPaymentPostBuffer.Amount;
            IF Amount > 0 THEN
              "Debit Amount" := Amount
            ELSE
              "Credit Amount" := -Amount;
            "Amount (LCY)" := TempPaymentPostBuffer."Amount (LCY)";
            "Currency Factor" := TempPaymentPostBuffer."Currency Factor";
            IF ("Currency Factor" = 0) AND (Amount <> 0) THEN
              "Currency Factor" := Amount / "Amount (LCY)";
            Cust2.GET(GenPayLine."Account No.");
            VALIDATE(GenPayLine."Bank Account Code",Cust2."Preferred Bank Account");
            "Payment Class" := GenPayHead."Payment Class";
            VALIDATE("Status No.");
            "Posting Date" := PostingDate;
            IF SummarizePer = SummarizePer::" " THEN BEGIN
              "Applies-to Doc. Type" := CustLedgEntry."Document Type";
              "Applies-to Doc. No." := CustLedgEntry."Document No.";
              IF SEPADirectDebitMandate.GET(CustLedgEntry."Direct Debit Mandate ID") THEN
                VALIDATE("Bank Account Code",SEPADirectDebitMandate."Customer Bank Account Code");
              "Direct Debit Mandate ID" := CustLedgEntry."Direct Debit Mandate ID";
            END;
            CASE SummarizePer OF
              SummarizePer::" " :
                "Due Date" := CustLedgEntry."Due Date";
              SummarizePer::Customer :
                BEGIN
                  PayableCustLedgEntry.SETCURRENTKEY("Vendor No.","Due Date");
                  PayableCustLedgEntry.SETRANGE("Vendor No.",TempPaymentPostBuffer."Account No.");
                  PayableCustLedgEntry.FIND('+');
                  "Due Date" := PayableCustLedgEntry."Due Date";
                  PayableCustLedgEntry.DELETEALL;
                END;
              SummarizePer::"Due date" :
                "Due Date" := TempPaymentPostBuffer."Due Date";
            END;
            IF Amount <> 0 THEN
              INSERT;
            GenPayLineInserted := TRUE;
          END;
        UNTIL TempPaymentPostBuffer.NEXT = 0;
    END;

    LOCAL PROCEDURE ShowMessage@1120000(VAR Text@1120000 : Text[250]);
    BEGIN
      IF (Text <> '') AND GenPayLineInserted THEN
        MESSAGE(Text);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

