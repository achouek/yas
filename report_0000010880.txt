OBJECT Report 10880 ETEBAC Files
{
  OBJECT-PROPERTIES
  {
    Date=23/09/13;
    Time=12:00:00;
    Version List=NAVFR7.10;
  }
  PROPERTIES
  {
    CaptionML=[ENU=ETEBAC Files;
               FRA=Fichiers ETEBAC];
    ProcessingOnly=Yes;
    OnPreReport=VAR
                  FileMgt@1120000 : Codeunit 419;
                BEGIN
                  CompanyInfo.GET;
                  CompanyInfo.TESTFIELD(Name);

                  GLSetup.GET;

                  ExportFileName := FileMgt.ServerTempFileName('');

                  ExportFile.TEXTMODE := TRUE;
                  ExportFile.WRITEMODE := TRUE;
                  ExportFile.CREATE(ExportFileName);
                END;

    OnPostReport=VAR
                   ToFile@1120000 : Text[260];
                 BEGIN
                   ExportFile.CLOSE;
                   ToFile := Text009;
                   DOWNLOAD(ExportFileName,GetCaption,'',Text008,ToFile);
                 END;

  }
  DATASET
  {
    { 6483;    ;DataItem;                    ;
               DataItemTable=Table10865;
               DataItemTableView=SORTING(No.);
               MaxIteration=1;
               OnAfterGetRecord=VAR
                                  RecordCode@1120000 : Text[2];
                                  OperationCode@1120001 : Text[2];
                                  Numbering@1120002 : Text[8];
                                  FromPaymentNo@1120003 : Text[6];
                                  DeliveryDate@1120004 : Text[6];
                                  CompanyName@1120005 : Text[24];
                                  CompanyBankName@1120006 : Text[24];
                                  EntryCode@1120007 : Text[1];
                                  CurrencyIdentifier@1120008 : Code[1];
                                  BankBranchNo@1120009 : Text[5];
                                  AgencyCode@1120010 : Text[5];
                                  BankAccountNo@1120011 : Text[11];
                                  ExportedText@1120012 : Text[160];
                                BEGIN
                                  IF STRLEN("Bank Branch No.") > 5 THEN
                                    ERROR(Text002,"Account No.");

                                  IF STRLEN("Agency Code") > 5 THEN
                                    ERROR(Text002,"Account No.");

                                  IF STRLEN("Bank Account No.") > 11 THEN
                                    ERROR(Text002,"Account No.");

                                  IF NOT "RIB Checked" THEN
                                    ERROR(Text003,"Account No.");

                                  CASE "Currency Code" = '' OF
                                    TRUE:
                                      CurrencyUsed := '';
                                    FALSE:
                                      CurrencyUsed := 'FRF';
                                  END;

                                  CASE CurrencyUsed = '' OF
                                    TRUE:
                                      CurrencyIdentifier := 'E';
                                    FALSE:
                                      CurrencyIdentifier := 'F';
                                  END;

                                  LineNo := 1;

                                  CALCFIELDS(Amount);

                                  RecordCode := '03';
                                  OperationCode := '60';
                                  Numbering := CONVERTSTR(FORMAT(LineNo,8),' ','0');
                                  FromPaymentNo := PADSTR("National Issuer No.",6);
                                  DeliveryDate := FORMAT("Posting Date",6,'<Day,2><Month,2><Year,2>');
                                  CompanyName := PADSTR(CompanyInfo.Name,24);
                                  CompanyBankName := PADSTR("Bank Name",24);
                                  EntryCode := FORMAT(PaymentType + 1);
                                  BankBranchNo := PADSTR2("Bank Branch No.",5,'0');
                                  AgencyCode := PADSTR2("Agency Code",5,'0');
                                  BankAccountNo := PADSTR2("Bank Account No.",11,'0');
                                  ExportedText :=
                                    RecordCode +
                                    OperationCode +
                                    Numbering +
                                    FromPaymentNo +
                                    PADSTR('',6) +
                                    DeliveryDate +
                                    CompanyName +
                                    CompanyBankName +
                                    EntryCode +
                                    PADSTR('',1) +
                                    CurrencyIdentifier +
                                    BankBranchNo +
                                    AgencyCode +
                                    BankAccountNo;

                                  ExportFile.WRITE(PADSTR(ExportedText,160));
                                END;
                                 }

    { 3474;1   ;DataItem;                    ;
               DataItemTable=Table10866;
               DataItemTableView=SORTING(No.,Line No.);
               OnAfterGetRecord=VAR
                                  Cust@1120001 : Record 18;
                                  RecordCode@1120000 : Text[2];
                                  OperationCode@1120002 : Text[2];
                                  Numbering@1120003 : Text[8];
                                  BillReference@1120004 : Text[10];
                                  CustomerName@1120005 : Text[24];
                                  CustomerBankName@1120006 : Text[24];
                                  AcceptationCode@1120007 : Text[1];
                                  BankBranchNo@1120008 : Text[5];
                                  AgencyCode@1120009 : Text[5];
                                  BankAccountNo@1120010 : Text[11];
                                  PrintAmount@1120011 : Text[12];
                                  DueDate@1120012 : Text[6];
                                  CreationDate@1120013 : Text[6];
                                  ExportedText@1120014 : Text[160];
                                BEGIN
                                  IF (CurrencyUsed = '') AND ("Currency Code" <> '') THEN
                                    ERROR(Text004,GLSetup."LCY Code");

                                  TESTFIELD("Account No.");
                                  Cust.GET("Account No.");

                                  IF STRLEN("Bank Branch No.") > 5 THEN
                                    ERROR(Text006,"Bank Account Code");

                                  IF STRLEN("Agency Code") > 5 THEN
                                    ERROR(Text006,"Bank Account Code");

                                  IF STRLEN("Bank Account No.") > 11 THEN
                                    ERROR(Text006,"Bank Account Code");

                                  IF NOT "RIB Checked" THEN
                                    ERROR(Text007,"Bank Account Code","Account No.");

                                  LineNo := LineNo + 1;

                                  RecordCode := '06';
                                  OperationCode := '60';
                                  Numbering := CONVERTSTR(FORMAT(LineNo,8),' ','0');

                                  CASE "Drawee Reference" <> '' OF
                                    TRUE:
                                      BillReference := PADSTR2("Drawee Reference",10,'0');
                                    FALSE:
                                      BillReference := PADSTR2('',10,' ');
                                  END;

                                  CustomerName := PADSTR(Cust.Name,24);
                                  CustomerBankName := PADSTR("Bank Account Name",24);

                                  CASE "Acceptation Code" OF
                                    "Acceptation Code"::"LCR NA":
                                      AcceptationCode := '0';
                                    "Acceptation Code"::LCR:
                                      AcceptationCode := '1';
                                    "Acceptation Code"::BOR:
                                      AcceptationCode := '2'
                                  END;

                                  BankBranchNo := PADSTR2("Bank Branch No.",5,'0');
                                  AgencyCode := PADSTR2("Agency Code",5,'0');
                                  BankAccountNo := PADSTR2("Bank Account No.",11,'0');
                                  PrintAmount := FormatAmount(Amount,12);
                                  DueDate := FORMAT("Due Date",6,'<Day,2><Month,2><Year,2>');
                                  CreationDate := FORMAT("Payment Header"."Posting Date",6,'<Day,2><Month,2><Year,2>');
                                  ExportedText :=
                                    RecordCode +
                                    OperationCode +
                                    Numbering +
                                    PADSTR('',8) +
                                    BillReference +
                                    CustomerName +
                                    CustomerBankName +
                                    AcceptationCode +
                                    PADSTR('',2) +
                                    BankBranchNo +
                                    AgencyCode +
                                    BankAccountNo +
                                    PrintAmount +
                                    PADSTR('',4) +
                                    DueDate +
                                    CreationDate;

                                  ExportFile.WRITE(PADSTR(ExportedText,160));
                                END;

               OnPostDataItem=VAR
                                PaymentHeader@1120005 : Record 10865;
                                RecordCode@1120000 : Text[2];
                                OperationCode@1120001 : Text[2];
                                Numbering@1120002 : Text[8];
                                PrintAmount@1120003 : Text[12];
                                ExportedText@1120004 : Text[160];
                              BEGIN
                                LineNo := LineNo + 1;
                                RecordCode := '08';
                                OperationCode := '60';
                                Numbering := CONVERTSTR(FORMAT(LineNo,8),' ','0');
                                PrintAmount := FormatAmount("Payment Header".Amount,12);
                                ExportedText :=
                                  RecordCode +
                                  OperationCode +
                                  Numbering +
                                  PADSTR('',90) +
                                  PrintAmount;

                                ExportFile.WRITE(PADSTR(ExportedText,160));

                                PaymentHeader := "Payment Header";
                                PaymentHeader."File Export Completed" := TRUE;
                                PaymentHeader.MODIFY;
                              END;

               DataItemLink=No.=FIELD(No.) }

  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      OnInit=BEGIN
               FileNameVisible := TRUE;
             END;

      OnOpenPage=BEGIN
                   PaymentType := PaymentType::"Cash after Due Date";
                   FileNameVisible := FALSE;
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             FRA=Options] }

      { 1120000;2;Field     ;
                  Name=FileName;
                  CaptionML=[ENU=File Name;
                             FRA=Nom du fichier];
                  SourceExpr=ExportFileName;
                  Visible=FileNameVisible;
                  OnAssistEdit=VAR
                                 FileMgt@1120000 : Codeunit 419;
                               BEGIN
                                 ExportFileName := FileMgt.SaveFileDialog(GetCaption,ExportFileName,Text008);
                               END;
                                }

      { 1120002;2;Field     ;
                  CaptionML=[ENU=Payment Type;
                             FRA=Type de rŠglement];
                  OptionCaptionML=[ENU=Discount,Discount in Value,Cash after Due Date,Cash after Unpaid Delay;
                                   FRA=Escompte,Escompte en valeur,Encaissement ‚ch‚ance,Encaissement aprŠs d‚lai d'impay‚];
                  SourceExpr=PaymentType }

    }
  }
  LABELS
  {
  }
  CODE
  {
    VAR
      GLSetup@1120010 : Record 98;
      CompanyInfo@1120009 : Record 79;
      ExportFile@1120012 : File;
      ExportFileName@1120000 : Text[260];
      CurrencyUsed@1120014 : Code[10];
      PaymentType@1120001 : 'Discount,Discount in Value,Cash after Due Date,Cash after Unpaid Delay';
      Text002@1120003 : TextConst 'ENU=Bank Account No. %1 is too long. Please verify before continuing.;FRA=Le nø de compte bancaire %1 est trop long. V‚rifiez-le avant de continuer.';
      Text003@1120004 : TextConst 'ENU=The RIB of the company''s bank account %1 is incorrect. Please verify before continuing.;FRA=Le RIB du compte bancaire %1 de la soci‚t‚ est erron‚. V‚rifiez-le avant de continuer.';
      Text004@1120005 : TextConst 'ENU=You can only use %1;FRA=Vous pouvez seulement utiliser %1';
      Text006@1120007 : TextConst 'ENU=Bank Account No. %1 is too long. Please verify before continuing.;FRA=Le nø de compte bancaire %1 est trop long. V‚rifiez-le avant de continuer.';
      Text007@1120008 : TextConst 'ENU=The RIB of bank account %1 of customer %2 is incorrect. Please verify before continuing.;FRA=Le RIB du compte bancaire %1 du client %2 est erron‚. V‚rifiez-le avant de continuer.';
      Text008@1120011 : TextConst 'ENU=Text Files|*.txt|All Files|*.*;FRA=Fichiers texte|*.txt|Tous les fichiers|*.*';
      Text009@1120013 : TextConst 'ENU=default.txt;FRA=default.txt';
      LineNo@1120015 : Integer;
      FileNameVisible@19066691 : Boolean INDATASET;

    LOCAL PROCEDURE GetCaption@1101100008() Result : Text[50];
    VAR
      AllObjWithCaption@1101100000 : Record 2000000058;
      ID@1101100001 : Integer;
    BEGIN
      IF NOT EVALUATE(ID,DELCHR(CurrReport.OBJECTID(FALSE),'=',DELCHR(CurrReport.OBJECTID(FALSE),'=','0123456789'))) THEN
        EXIT;

      IF NOT AllObjWithCaption.GET(AllObjWithCaption."Object Type"::Report,ID) THEN
        EXIT;

      EXIT(COPYSTR(AllObjWithCaption."Object Caption",1,MAXSTRLEN(Result)));
    END;

    LOCAL PROCEDURE PADSTR2@1120016(String@1120000 : Text[1024];Length@1120001 : Integer;FillCharacter@1120002 : Text[1]) : Text[1024];
    VAR
      PaddingLength@1120003 : Integer;
    BEGIN
      PaddingLength := Length - STRLEN(String);

      CASE TRUE OF
        PaddingLength <= 0:
          EXIT(PADSTR(String,Length,FillCharacter));
        PaddingLength > 0:
          EXIT(PADSTR('',PaddingLength,FillCharacter) + String);
      END;
    END;

    LOCAL PROCEDURE FormatAmount@1120055(Amount@1120000 : Decimal;Width@1120001 : Integer) : Text[50];
    VAR
      FormatAmount@1120002 : Text[50];
    BEGIN
      FormatAmount := CONVERTSTR(FORMAT(Amount,Width,'<Precision,2:2><Integer><Decimal><Comma,,>'),' ','0');
      FormatAmount := '0' + COPYSTR(FormatAmount,1,Width - 3) + COPYSTR(FormatAmount,Width - 1,2);
      EXIT(FormatAmount);
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}

