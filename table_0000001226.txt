OBJECT Table 1226 Payment Export Data
{
  OBJECT-PROPERTIES
  {
    Date=30/01/14;
    Time=12:00:00;
    Version List=NAVW17.10.00.36078;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF NOT PreserveNonLatinCharacters THEN
                 PaymentExportConvertToLatin;
             END;

    CaptionML=[ENU=Payment Export Data;
               FRA=Donn‚es exportation paiement];
  }
  FIELDS
  {
    { 1   ;   ;Entry No.           ;Integer       ;AutoIncrement=Yes;
                                                   CaptionML=[ENU=Entry No.;
                                                              FRA=Nø s‚quence] }
    { 2   ;   ;Posting Exch Entry No.;Integer     ;TableRelation="Posting Exch.";
                                                   CaptionML=[ENU=Posting Exch Entry No.;
                                                              FRA=Nø ‚criture ‚chge compta.] }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              FRA=Nø ligne] }
    { 4   ;   ;Posting Exch. Line Def Code;Code20 ;CaptionML=[ENU=Posting Exch. Line Def Code;
                                                              FRA=Code appellation ligne ‚chge compta.] }
    { 5   ;   ;General Journal Template;Code10    ;TableRelation="Gen. Journal Template";
                                                   CaptionML=[ENU=General Journal Template;
                                                              FRA=ModŠle feuille comptabilit‚] }
    { 6   ;   ;General Journal Batch Name;Code10  ;TableRelation="Gen. Journal Batch";
                                                   CaptionML=[ENU=General Journal Batch Name;
                                                              FRA=Nom feuille comptabilit‚] }
    { 7   ;   ;General Journal Line No.;Integer   ;CaptionML=[ENU=General Journal Line No.;
                                                              FRA=Nø ligne feuille comptabilit‚] }
    { 30  ;   ;Sender Bank Account Code;Code20    ;TableRelation="Bank Account";
                                                   CaptionML=[ENU=Sender Bank Account Code;
                                                              FRA=Code cpte bancaire ‚metteur] }
    { 31  ;   ;Sender Bank Account No.;Text50     ;CaptionML=[ENU=Sender Bank Account No.;
                                                              FRA=Nø cpte bancaire ‚metteur] }
    { 34  ;   ;Sender Bank BIC     ;Code35        ;CaptionML=[ENU=Sender Bank BIC;
                                                              FRA=BIC banque ‚metteur] }
    { 40  ;   ;Recipient Name      ;Text50        ;CaptionML=[ENU=Recipient Name;
                                                              FRA=Nom destinataire] }
    { 41  ;   ;Recipient Address   ;Text50        ;CaptionML=[ENU=Recipient Address;
                                                              FRA=Adresse destinataire] }
    { 42  ;   ;Recipient City      ;Text50        ;CaptionML=[ENU=Recipient City;
                                                              FRA=Ville destinataire] }
    { 43  ;   ;Recipient Post Code ;Code20        ;CaptionML=[ENU=Recipient Post Code;
                                                              FRA=Code postal destinataire] }
    { 44  ;   ;Recipient Country/Region Code;Code10;
                                                   CaptionML=[ENU=Recipient Country/Region Code;
                                                              FRA=Code pays/r‚gion destinataire] }
    { 50  ;   ;Recipient Reg. No.  ;Code4         ;CaptionML=[ENU=Recipient Reg. No.;
                                                              FRA=Nø enreg. destinataire] }
    { 51  ;   ;Recipient Acc. No.  ;Code10        ;CaptionML=[ENU=Recipient Acc. No.;
                                                              FRA=Nø cpte destinataire] }
    { 52  ;   ;Recipient Bank Acc. No.;Text50     ;CaptionML=[ENU=Recipient Bank Acc. No.;
                                                              FRA=Nø cpte bancaire destinataire] }
    { 53  ;   ;Recipient Bank BIC  ;Code35        ;CaptionML=[ENU=Recipient Bank BIC;
                                                              FRA=BIC banque destinataire] }
    { 54  ;   ;Recipient Bank Name ;Text50        ;CaptionML=[ENU=Recipient Bank Name;
                                                              FRA=Nom banque destinataire] }
    { 55  ;   ;Recipient Bank Address;Text50      ;CaptionML=[ENU=Recipient Bank Address;
                                                              FRA=Adresse banque destinataire] }
    { 56  ;   ;Recipient Bank City ;Text50        ;CaptionML=[ENU=Recipient Bank City;
                                                              FRA=Ville banque destinataire] }
    { 57  ;   ;Recipient Bank Country/Region;Code10;
                                                   TableRelation=Country/Region;
                                                   CaptionML=[ENU=Recipient Bank Country/Region;
                                                              FRA=Pays/r‚gion banque destinataire] }
    { 58  ;   ;Recipient Creditor No.;Code20      ;CaptionML=[ENU=Recipient Creditor No.;
                                                              FRA=Nø cr‚diteur destinataire] }
    { 59  ;   ;Recipient Bank Post Code;Code20    ;CaptionML=[ENU=Recipient Bank Post Code;
                                                              FRA=Code postal banque destinataire] }
    { 60  ;   ;Message Type        ;Code1         ;CaptionML=[ENU=Message Type;
                                                              FRA=Type message] }
    { 61  ;   ;Letter to Sender    ;Code1         ;CaptionML=[ENU=Letter to Sender;
                                                              FRA=Lettre … l'‚metteur] }
    { 63  ;   ;Recipient Acknowledgement;Code1    ;CaptionML=[ENU=Recipient Acknowledgement;
                                                              FRA=Approbation destinataire] }
    { 64  ;   ;Short Advice        ;Text20        ;CaptionML=[ENU=Short Advice;
                                                              FRA=Avis bref] }
    { 65  ;   ;Message to Recipient 1;Text140     ;CaptionML=[ENU=Message to Recipient 1;
                                                              FRA=Message au destinataireÿ1] }
    { 66  ;   ;Message to Recipient 2;Text140     ;CaptionML=[ENU=Message to Recipient 2;
                                                              FRA=Message au destinataireÿ2] }
    { 80  ;   ;Amount              ;Decimal       ;CaptionML=[ENU=Amount;
                                                              FRA=Montant];
                                                   DecimalPlaces=2:2 }
    { 81  ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   CaptionML=[ENU=Currency Code;
                                                              FRA=Code devise] }
    { 82  ;   ;Transfer Date       ;Date          ;CaptionML=[ENU=Transfer Date;
                                                              FRA=Date transfert] }
    { 83  ;   ;Transfer Type       ;Code1         ;CaptionML=[ENU=Transfer Type;
                                                              FRA=Type transfert] }
    { 85  ;   ;Document No.        ;Code20        ;CaptionML=[ENU=Document No.;
                                                              FRA=Nø document] }
    { 87  ;   ;Recipient Reference ;Code35        ;CaptionML=[ENU=Recipient Reference;
                                                              FRA=R‚f‚rence destinataire] }
    { 88  ;   ;Payment Reference   ;Code50        ;CaptionML=[ENU=Payment Reference;
                                                              FRA=R‚f‚rence paiement] }
    { 100 ;   ;SEPA Payment Information ID;Text35 ;CaptionML=[ENU=SEPA Payment Information ID;
                                                              FRA=ID informations paiement SEPA] }
    { 101 ;   ;SEPA End-to-End ID  ;Text35        ;CaptionML=[ENU=SEPA End-to-End ID;
                                                              FRA=ID bout en bout SEPA] }
    { 102 ;   ;SEPA Message ID     ;Text35        ;CaptionML=[ENU=SEPA Message ID;
                                                              FRA=ID message SEPA] }
    { 103 ;   ;SEPA Instruction Priority;Option   ;OnValidate=BEGIN
                                                                CASE "SEPA Instruction Priority" OF
                                                                  "SEPA Instruction Priority"::NORMAL:
                                                                    "SEPA Instruction Priority Text" := 'NORM';
                                                                  "SEPA Instruction Priority"::HIGH:
                                                                    "SEPA Instruction Priority Text" := 'HIGH';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=SEPA Instruction Priority;
                                                              FRA=Priorit‚ instruction SEPA];
                                                   OptionCaptionML=[ENU=NORMAL,HIGH;
                                                                    FRA=NORMAL,HIGH];
                                                   OptionString=NORMAL,HIGH }
    { 104 ;   ;SEPA Instruction Priority Text;Code4;
                                                   CaptionML=[ENU=SEPA Instruction Priority Text;
                                                              FRA=Texte priorit‚ instruction SEPA];
                                                   Editable=No }
    { 105 ;   ;SEPA Payment Method ;Option        ;InitValue=TRF;
                                                   OnValidate=BEGIN
                                                                CASE "SEPA Payment Method" OF
                                                                  "SEPA Payment Method"::CHK:
                                                                    "SEPA Payment Method Text" := 'CHK';
                                                                  "SEPA Payment Method"::TRF:
                                                                    "SEPA Payment Method Text" := 'TRF';
                                                                  "SEPA Payment Method"::TRA:
                                                                    "SEPA Payment Method Text" := 'TRA';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=SEPA Payment Method;
                                                              FRA=Mode de rŠglement SEPA];
                                                   OptionCaptionML=[ENU=CHK,TRF,TRA;
                                                                    FRA=CHK,TRF,TRA];
                                                   OptionString=CHK,TRF,TRA }
    { 106 ;   ;SEPA Payment Method Text;Code3     ;CaptionML=[ENU=SEPA Payment Method Text;
                                                              FRA=Texte mode de rŠglement SEPA] }
    { 107 ;   ;SEPA Batch Booking  ;Boolean       ;CaptionML=[ENU=SEPA Batch Booking;
                                                              FRA=R‚servation lot SEPA] }
    { 108 ;   ;SEPA Charge Bearer  ;Option        ;InitValue=SLEV;
                                                   OnValidate=BEGIN
                                                                CASE "SEPA Charge Bearer" OF
                                                                  "SEPA Charge Bearer"::DEBT:
                                                                    "SEPA Charge Bearer Text" := 'DEBT';
                                                                  "SEPA Charge Bearer"::CRED:
                                                                    "SEPA Charge Bearer Text" := 'CRED';
                                                                  "SEPA Charge Bearer"::SHAR:
                                                                    "SEPA Charge Bearer Text" := 'SHAR';
                                                                  "SEPA Charge Bearer"::SLEV:
                                                                    "SEPA Charge Bearer Text" := 'SLEV';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=SEPA Charge Bearer;
                                                              FRA=Personne prenant en charge les frais SEPA];
                                                   OptionCaptionML=[ENU=DEBT,CRED,SHAR,SLEV;
                                                                    FRA=DEBT,CRED,SHAR,SLEV];
                                                   OptionString=DEBT,CRED,SHAR,SLEV }
    { 109 ;   ;SEPA Charge Bearer Text;Code4      ;CaptionML=[ENU=SEPA Charge Bearer Text;
                                                              FRA=Texte personne prenant en charge les frais SEPA] }
    { 120 ;   ;SEPA Direct Debit Mandate ID;Code35;CaptionML=[ENU=SEPA Direct Debit Mandate ID;
                                                              FRA=ID mandat de pr‚lŠvement SEPA] }
    { 121 ;   ;SEPA Direct Debit Seq. Type;Option ;OnValidate=BEGIN
                                                                CASE "SEPA Direct Debit Seq. Type" OF
                                                                  "SEPA Direct Debit Seq. Type"::"One Off":
                                                                    "SEPA Direct Debit Seq. Text" := 'OOFF';
                                                                  "SEPA Direct Debit Seq. Type"::First:
                                                                    "SEPA Direct Debit Seq. Text" := 'FRST';
                                                                  "SEPA Direct Debit Seq. Type"::Recurring:
                                                                    "SEPA Direct Debit Seq. Text" := 'RCUR';
                                                                  "SEPA Direct Debit Seq. Type"::Last:
                                                                    "SEPA Direct Debit Seq. Text" := 'FNAL';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=SEPA Direct Debit Seq. Type;
                                                              FRA=Type paiem. pr‚lŠvement SEPA];
                                                   OptionCaptionML=[ENU=One Off,First,Recurring,Last;
                                                                    FRA=Ponctuel,Premier,R‚current,Dernier];
                                                   OptionString=One Off,First,Recurring,Last }
    { 122 ;   ;SEPA Direct Debit Seq. Text;Code4  ;CaptionML=[ENU=SEPA Direct Debit Seq. Text;
                                                              FRA=Texte paiem. pr‚lŠvement SEPA] }
    { 123 ;   ;SEPA DD Mandate Signed Date;Date   ;CaptionML=[ENU=SEPA DD Mandate Signed Date;
                                                              FRA=Date signature mandat pr‚l. SEPA] }
    { 124 ;   ;SEPA Partner Type   ;Option        ;OnValidate=BEGIN
                                                                CASE "SEPA Partner Type" OF
                                                                  "SEPA Partner Type"::" ":
                                                                    "SEPA Partner Type Text" := '';
                                                                  "SEPA Partner Type"::Company:
                                                                    "SEPA Partner Type Text" := 'B2B';
                                                                  "SEPA Partner Type"::Person:
                                                                    "SEPA Partner Type Text" := 'CORE';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=SEPA Partner Type;
                                                              FRA=Type partenaire SEPA];
                                                   OptionCaptionML=[ENU=" ,Company,Person";
                                                                    FRA=" ,Soci‚t‚,Personne"];
                                                   OptionString=[ ,Company,Person] }
    { 125 ;   ;SEPA Partner Type Text;Code4       ;CaptionML=[ENU=SEPA Partner Type Text;
                                                              FRA=Texte type partenaire SEPA] }
    { 170 ;   ;Creditor No.        ;Code35        ;CaptionML=[ENU=Creditor No.;
                                                              FRA=Creditor No.] }
    { 173 ;   ;Applies-to Ext. Doc. No.;Code35    ;CaptionML=[ENU=Applies-to Ext. Doc. No.;
                                                              FRA=Nø ligne doc. ext. lettrage] }
    { 200 ;   ;Format Command      ;Code4         ;CaptionML=[ENU=Format Command;
                                                              FRA=Commande Mettre en forme] }
    { 201 ;   ;Format Remittance Info Type;Code1  ;CaptionML=[ENU=Format Remittance Info Type;
                                                              FRA=Mettre en forme type info remise] }
    { 220 ;   ;Format Payment Type ;Code2         ;CaptionML=[ENU=Format Payment Type;
                                                              FRA=Mettre en forme mode paiement] }
    { 221 ;   ;Format Expense Code ;Code1         ;CaptionML=[ENU=Format Expense Code;
                                                              FRA=Mettre en forme code d‚pense] }
    { 222 ;   ;Format Text Code    ;Code3         ;CaptionML=[ENU=Format Text Code;
                                                              FRA=Mettre en forme code texte] }
    { 283 ;   ;Format Form Type    ;Code2         ;CaptionML=[ENU=Format Form Type;
                                                              FRA=Mettre en forme type formulaire] }
  }
  KEYS
  {
    {    ;Entry No.                               ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      TempPaymentExportRemittanceText@1001 : TEMPORARY Record 1229;
      PreserveNonLatinCharacters@1004 : Boolean;

    PROCEDURE InitData@1(VAR GenJnlLine@1000 : Record 81);
    BEGIN
      RESET;
      CLEAR(TempPaymentExportRemittanceText);
      INIT;
      Amount := GenJnlLine.Amount;
      "Currency Code" := GenJnlLine."Currency Code";
    END;

    PROCEDURE AddRemittanceText@3(NewText@1000 : Text[140]);
    BEGIN
      IF NewText = '' THEN
        EXIT;
      IF TempPaymentExportRemittanceText.FINDLAST THEN;
      TempPaymentExportRemittanceText."Pmt. Export Data Entry No." := "Entry No.";
      TempPaymentExportRemittanceText."Line No." += 1;
      IF PreserveNonLatinCharacters THEN
        TempPaymentExportRemittanceText.Text := NewText
      ELSE
        TempPaymentExportRemittanceText.Text := COPYSTR(ConvertToASCII(NewText),1,MAXSTRLEN(TempPaymentExportRemittanceText.Text));
      TempPaymentExportRemittanceText.INSERT;
    END;

    PROCEDURE GetRemittanceTexts@5(VAR PaymentExportRemittanceText@1000 : Record 1229);
    BEGIN
      IF TempPaymentExportRemittanceText.FINDSET THEN
        REPEAT
          PaymentExportRemittanceText := TempPaymentExportRemittanceText;
          PaymentExportRemittanceText.INSERT;
        UNTIL TempPaymentExportRemittanceText.NEXT = 0;
    END;

    PROCEDURE AddGenJnlLineErrorText@8(GenJnlLine@1001 : Record 81;NewText@1000 : Text);
    BEGIN
      GenJnlLine.InsertPaymentFileError(NewText);
    END;

    LOCAL PROCEDURE ConvertToASCII@9(Text@1000 : Text) : Text;
    VAR
      StringConversionManagement@1004 : Codeunit 47;
    BEGIN
      EXIT(StringConversionManagement.WindowsToASCII(Text));
    END;

    PROCEDURE SetPreserveNonLatinCharacters@15(NewPreserveNonLatinCharacters@1000 : Boolean);
    BEGIN
      PreserveNonLatinCharacters := NewPreserveNonLatinCharacters;
    END;

    PROCEDURE GetPreserveNonLatinCharacters@17() : Boolean;
    BEGIN
      EXIT(PreserveNonLatinCharacters);
    END;

    LOCAL PROCEDURE PaymentExportConvertToLatin@22();
    VAR
      RecRef@1001 : RecordRef;
    BEGIN
      RecRef.GETTABLE(Rec);
      ConvertFieldsToLatinCharacters(RecRef);
      RecRef.SETTABLE(Rec);
    END;

    PROCEDURE CompanyInformationConvertToLatin@20(VAR CompanyInformation@1000 : Record 79);
    VAR
      RecRef@1001 : RecordRef;
    BEGIN
      RecRef.GETTABLE(CompanyInformation);
      ConvertFieldsToLatinCharacters(RecRef);
      RecRef.SETTABLE(CompanyInformation);
    END;

    LOCAL PROCEDURE ConvertFieldsToLatinCharacters@19(VAR RecRef@1001 : RecordRef);
    VAR
      StringConversionManagement@1004 : Codeunit 47;
      FieldRef@1002 : FieldRef;
      i@1003 : Integer;
    BEGIN
      PreserveNonLatinCharacters := FALSE;
      FOR i := 1 TO RecRef.FIELDCOUNT DO BEGIN
        FieldRef := RecRef.FIELDINDEX(i);
        IF (FORMAT(FieldRef.CLASS) = 'Normal') AND (FORMAT(FieldRef.TYPE) IN ['Text','Code']) THEN
          FieldRef.VALUE := COPYSTR(StringConversionManagement.WindowsToASCII(FORMAT(FieldRef.VALUE)),1,FieldRef.LENGTH);
      END;
    END;

    PROCEDURE SetCustomerAsRecipient@2(VAR Customer@1000 : Record 18;VAR CustomerBankAccount@1001 : Record 287);
    BEGIN
      CustomerBankAccount.TESTFIELD(IBAN);
      CustomerBankAccount.TESTFIELD("SWIFT Code");

      "Recipient Name" := Customer.Name;
      "Recipient Address" := Customer.Address;
      "Recipient City" := COPYSTR(Customer.City,1,35);
      "Recipient Post Code" := Customer."Post Code";
      "Recipient Country/Region Code" := Customer."Country/Region Code";
      "Recipient Bank Name" := CustomerBankAccount.Name;
      "Recipient Bank Address" := CustomerBankAccount.Address;
      "Recipient Bank City" := COPYSTR(CustomerBankAccount.City,1,35);
      "Recipient Bank Post Code" := CustomerBankAccount."Post Code";
      "Recipient Bank Country/Region" := CustomerBankAccount."Country/Region Code";
      "Recipient Bank BIC" := CustomerBankAccount."SWIFT Code";
      "Recipient Bank Acc. No." := DELCHR(CustomerBankAccount.IBAN,'=<>');
    END;

    PROCEDURE SetVendorAsRecipient@10(VAR Vendor@1000 : Record 23;VAR VendorBankAccount@1001 : Record 288);
    BEGIN
      VendorBankAccount.TESTFIELD(IBAN);
      VendorBankAccount.TESTFIELD("SWIFT Code");

      "Recipient Name" := Vendor.Name;
      "Recipient Address" := Vendor.Address;
      "Recipient City" := COPYSTR(Vendor.City,1,35);
      "Recipient Post Code" := Vendor."Post Code";
      "Recipient Country/Region Code" := Vendor."Country/Region Code";
      "Recipient Bank Name" := VendorBankAccount.Name;
      "Recipient Bank Address" := VendorBankAccount.Address;
      "Recipient Bank City" := COPYSTR(VendorBankAccount.City,1,35);
      "Recipient Bank Post Code" := VendorBankAccount."Post Code";
      "Recipient Bank Country/Region" := VendorBankAccount."Country/Region Code";
      "Recipient Bank BIC" := VendorBankAccount."SWIFT Code";
      "Recipient Bank Acc. No." := DELCHR(VendorBankAccount.IBAN,'=<>');
    END;

    PROCEDURE SetBankAsSenderBank@11(BankAccount@1000 : Record 270);
    BEGIN
      BankAccount.TESTFIELD(IBAN);
      BankAccount.TESTFIELD("SWIFT Code");

      "Sender Bank Account Code" := BankAccount."No.";
      "Sender Bank Account No." := DELCHR(BankAccount.IBAN,'=<>');
      "Sender Bank BIC" := BankAccount."SWIFT Code";
    END;

    PROCEDURE SetCreditorIdentifier@6(BankAccount@1000 : Record 270);
    BEGIN
      BankAccount.TESTFIELD("Creditor No.");
      "Creditor No." := BankAccount."Creditor No.";
    END;

    PROCEDURE IsFieldBlank@4(FieldID@1003 : Integer) : Boolean;
    VAR
      RecRef@1005 : RecordRef;
      FieldRef@1002 : FieldRef;
      BlankValue@1000 : Text;
    BEGIN
      RecRef.GETTABLE(Rec);
      FieldRef := RecRef.FIELD(FieldID);
      CASE FORMAT(FieldRef.TYPE) OF
        'Text','Code','Date':
          BlankValue := '';
        'Decimal','Integer':
          BlankValue := '0';
      END;
      EXIT(FORMAT(FieldRef.VALUE) = BlankValue);
    END;

    BEGIN
    END.
  }
}

