OBJECT Table 5404 Item Unit of Measure
{
  OBJECT-PROPERTIES
  {
    Date=10/05/14;
    Time=12:00:00;
    Version List=NAVW17.10.00.36703;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               TestItemSetup;
               TestNoOpenEntriesExist;
             END;

    OnRename=BEGIN
               TestItemSetup;
               TestNoOpenEntriesExist;
             END;

    CaptionML=[ENU=Item Unit of Measure;
               FRA=Unit‚ article];
    LookupPageID=Page5404;
  }
  FIELDS
  {
    { 1   ;   ;Item No.            ;Code20        ;TableRelation=Item;
                                                   OnValidate=BEGIN
                                                                CalcWeight;
                                                              END;

                                                   CaptionML=[ENU=Item No.;
                                                              FRA=Nø article];
                                                   NotBlank=Yes }
    { 2   ;   ;Code                ;Code10        ;TableRelation="Unit of Measure";
                                                   CaptionML=[ENU=Code;
                                                              FRA=Code];
                                                   NotBlank=Yes }
    { 3   ;   ;Qty. per Unit of Measure;Decimal   ;InitValue=1;
                                                   OnValidate=BEGIN
                                                                IF "Qty. per Unit of Measure" <= 0 THEN
                                                                  FIELDERROR("Qty. per Unit of Measure",Text000);
                                                                IF xRec."Qty. per Unit of Measure" <> "Qty. per Unit of Measure" THEN
                                                                  CheckNoEntriesWithUoM;
                                                                Item.GET("Item No.");
                                                                IF Item."Base Unit of Measure" = Code THEN
                                                                  TESTFIELD("Qty. per Unit of Measure",1);
                                                                CalcWeight;
                                                              END;

                                                   CaptionML=[ENU=Qty. of Base Unit of Measure per Unit;
                                                              FRA=Qt‚ d'unit‚ de base par unit‚];
                                                   DecimalPlaces=0:5 }
    { 7300;   ;Length              ;Decimal       ;OnValidate=BEGIN
                                                                CalcCubage;
                                                              END;

                                                   CaptionML=[ENU=Length;
                                                              FRA=Longueur];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 7301;   ;Width               ;Decimal       ;OnValidate=BEGIN
                                                                CalcCubage;
                                                              END;

                                                   CaptionML=[ENU=Width;
                                                              FRA=Largeur];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 7302;   ;Height              ;Decimal       ;OnValidate=BEGIN
                                                                CalcCubage;
                                                              END;

                                                   CaptionML=[ENU=Height;
                                                              FRA=Hauteur];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 7303;   ;Cubage              ;Decimal       ;CaptionML=[ENU=Cubage;
                                                              FRA=Cubage];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
    { 7304;   ;Weight              ;Decimal       ;CaptionML=[ENU=Weight;
                                                              FRA=Poids];
                                                   DecimalPlaces=0:5;
                                                   MinValue=0 }
  }
  KEYS
  {
    {    ;Item No.,Code                           ;Clustered=Yes }
    {    ;Item No.,Qty. per Unit of Measure        }
    {    ;Code                                     }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Code,Qty. per Unit of Measure            }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=must be greater than 0;FRA=doit ˆtre sup‚rieur(e) … 0';
      Item@1001 : Record 27;
      Text001@1004 : TextConst 'ENU=You cannot rename %1 %2 for item %3 because it is the item''s %4 and there are one or more open ledger entries for the item.;FRA=Vous ne pouvez pas renommer l''enregistrement %1 %2 de l''article %3 car il s''agit du %4 de l''article et il existe une ou plusieurs ‚critures comptables ouvertes pour cet article.';
      CannotModifyBaseUnitOfMeasureErr@1005 : TextConst '@@@=%1 Table name (Item Unit of measure), %2 Value of Measure (KG, PCS...), %3 Item ID, %4 Base unit of Measure;ENU=You cannot modify %1 %2 for item %3 because it is the item''s %4.;FRA=Vous ne pouvez pas modifier %1 %2 de l''article %3, car il s''agit du %4 de l''article.';
      CannotModifyUnitOfMeasureErr@1002 : TextConst '@@@=%1 Table name (Item Unit of measure), %2 Value of Measure (KG, PCS...), %3 Item ID, %4 Entry Table Name;ENU=You cannot modify %1 %2 for item %3 because %4 exist.;FRA=You cannot modify %1 %2 for item %3 because %4 exist.';

    PROCEDURE CalcCubage@7300();
    BEGIN
      Cubage := Length * Width * Height;
    END;

    PROCEDURE CalcWeight@7301();
    BEGIN
      IF Item."No." <> "Item No." THEN
        Item.GET("Item No.");
      Weight := "Qty. per Unit of Measure" * Item."Net Weight";
    END;

    PROCEDURE TestNoOpenEntriesExist@1();
    VAR
      Item@1000 : Record 27;
      ItemLedgEntry@1001 : Record 32;
    BEGIN
      IF Item.GET("Item No.") THEN
        IF Item."Base Unit of Measure" = xRec.Code THEN BEGIN
          ItemLedgEntry.SETCURRENTKEY("Item No.",Open);
          ItemLedgEntry.SETRANGE("Item No.","Item No.");
          ItemLedgEntry.SETRANGE(Open,TRUE);
          IF NOT ItemLedgEntry.ISEMPTY THEN
            ERROR(Text001,TABLECAPTION,xRec.Code,"Item No.",Item.FIELDCAPTION("Base Unit of Measure"));
        END;
    END;

    PROCEDURE TestItemSetup@2();
    BEGIN
      IF Item.GET("Item No.") THEN BEGIN
        IF Item."Base Unit of Measure" = xRec.Code THEN
          ERROR(CannotModifyBaseUnitOfMeasureErr,TABLECAPTION,xRec.Code,"Item No.",Item.FIELDCAPTION("Base Unit of Measure"));
        IF Item."Sales Unit of Measure" = xRec.Code THEN
          ERROR(CannotModifyBaseUnitOfMeasureErr,TABLECAPTION,xRec.Code,"Item No.",Item.FIELDCAPTION("Sales Unit of Measure"));
        IF Item."Purch. Unit of Measure" = xRec.Code THEN
          ERROR(CannotModifyBaseUnitOfMeasureErr,TABLECAPTION,xRec.Code,"Item No.",Item.FIELDCAPTION("Purch. Unit of Measure"));
      END;
    END;

    PROCEDURE CheckNoEntriesWithUoM@3();
    VAR
      ItemLedgerEntry@1000 : Record 32;
    BEGIN
      ItemLedgerEntry.SETRANGE("Item No.","Item No.");
      ItemLedgerEntry.SETRANGE("Unit of Measure Code",Code);
      ItemLedgerEntry.SETRANGE(Open,TRUE);
      IF NOT ItemLedgerEntry.ISEMPTY THEN
        ERROR(CannotModifyUnitOfMeasureErr,TABLECAPTION,xRec.Code,"Item No.",ItemLedgerEntry.TABLECAPTION);
    END;

    BEGIN
    END.
  }
}

